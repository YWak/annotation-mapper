apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "eclipse"
apply plugin: "maven"

// 変数定義
def javaVersion = "1.8"
def defaultEncoding = "UTF-8"

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }
group = "info.motteke"
archivesBaseName="annotation-mapper"
version = new File("src/main/resources/version").text.trim()

repositories {
    mavenCentral()
    maven {
        url "http://maven.seasar.org/maven2"
    }
}

sourceSets {
    errorsTest {
        java.srcDir file('src/test/java_errors')
    }
    mapperTest {
        java {
            srcDir file('src/test/java_mappers')
            compileClasspath += test.output
            runtimeClasspath += test.output
        }
    }
}

configurations {
    all*.exclude group: "commons-logging"

    mapperTestCompile.extendsFrom testCompile
}

dependencies {
    testCompile (
        "junit:junit:4.12",
        "org.seasar.aptina:aptina-unit:1.0.0",
        "com.fasterxml.jackson.core:jackson-databind:2.9.0",
        "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.0",
        "com.googlecode.java-diff-utils:diffutils:1.3.0",
    )
}

compileTestJava {
    options.failOnError = false
}

compileMapperTestJava {
    destinationDir = compileTestJava.destinationDir
}

test {
    dependsOn compileMapperTestJava
    reports {
        html {
            enabled = true
            destination = "${buildDir}/reports/tests"
        }
        junitXml {
            enabled = true
            destination = "${buildDir}/test-results/tests"
            outputPerTestCase = false
        }
    }
}

jacocoTestReport {
    reports {
        sourceSets sourceSets.main
        xml {
            enabled = true
            destination = "${buildDir}/test-results/jacoco/coverage.xml"
        }
        html {
            enabled = true
            destination = "${buildDir}/reports/jacoco"
        }
    }
}

check.dependsOn += jacocoTestReport

javadoc {
    options {
        encoding = defaultEncoding
        docEncoding = defaultEncoding
        charSet = defaultEncoding
    }
}

eclipse {
    project {
        file {
            withXml {
                def resources = it.asNode().appendNode("filteredResources")
                def random = new Random()
                def create = { parent, type, arguments ->
                    def filter = parent.appendNode("filter")
                    filter.appendNode("id", System.currentTimeMillis() + random.nextInt())
                    filter.appendNode("type", type)
                    filter.appendNode("name", "")

                    def matcher = filter.appendNode("matcher")
                    matcher.appendNode("id", "org.eclipse.ui.ide.multiFilter")
                    matcher.appendNode("arguments", arguments)
                }

                create resources, 10, "1.0-name-matches-false-false-gradle"
                create resources,  6, '1.0-name-matches-false-false-gradlew*'
            }
        }
    }

    classpath {
        plusConfigurations += [ configurations.mapperTestCompile ]
    }
}

eclipseClasspath.dependsOn += cleanEclipseClasspath
eclipseProject.dependsOn += cleanEclipseProject

// デプロイ用
// cf. http://sassembla.github.io/Public/12:07:26%2023-19-43/12:07:26%2023-19-43.html
uploadArchives {
    repositories {
        mavenDeployer {
            repository (url: "file:${repoDir}")
        }
    }
}

[install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
    pom.project {
        inceptionYear '2017'
        packaging 'jar'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.1"
}
